package sintactico;

import backend.BackTables;
import c3a.GeneratorC3A;
import c3a.InstructionC3A.Code;
import errores.CompilerError;
import errores.SymTabError;
import generatorAssembler.GeneratorAssembler;
import java_cup.runtime.ComplexSymbolFactory;
import java_cup.runtime.ComplexSymbolFactory.ComplexSymbol;
import java_cup.runtime.ComplexSymbolFactory.Location;
import java_cup.runtime.Symbol;
import lexico.Lexico;
import core.*;
import symbolsTable.SymbolsTable;
import symbolsTable.Type;
import symbolsTable.Type.Tipo;
import symbolsTable.Type.TipoSubyacente;

import java.util.ArrayList;
import java.util.List;
import java.util.Stack;

class Parser;

parser code
{:
  private SymbolsTable symbolsTable;
  private BackTables backTables;
  private Lexico lexico;
  private GeneratorC3A c3a_g;
  private GeneratorAssembler assembler;
  private String filename;
  private String filenamePath;

  private Stack<String> labelTrueStack;
  private Stack<String> labelFalseStack;

  public Parser(Lexico lexico, ComplexSymbolFactory sf){
    super(lexico, sf);
    this.lexico = lexico;
    this.symbolsTable = new SymbolsTable();
    this.backTables = new BackTables(symbolsTable);
    this.c3a_g = new GeneratorC3A();
    this.assembler = new GeneratorAssembler(symbolsTable, backTables, c3a_g, "Example");
    this.labelTrueStack = new Stack<String>();
    this.labelFalseStack = new Stack<String>();
  }

  public Parser(Lexico lexico, ComplexSymbolFactory sf, String filename){
      super(lexico, sf);
      this.lexico = lexico;
      this.filename = filename;
      this.symbolsTable = new SymbolsTable(filename);
      this.backTables = new BackTables(symbolsTable, filename);
      this.c3a_g = new GeneratorC3A(filename);
      this.assembler = new GeneratorAssembler(symbolsTable, backTables, c3a_g, filename);
      this.labelTrueStack = new Stack<String>();
      this.labelFalseStack = new Stack<String>();
      filenamePath = filename;
      int pos = filenamePath.lastIndexOf(".");
      if (pos > 0) {
          filenamePath = filenamePath.substring(0, pos);
      }
    }


  public void syntax_error(Symbol s) {
    try {
      ArrayList<Integer> expected = new ArrayList<Integer>(expected_token_ids());
      ArrayList<String> expected_names = new ArrayList<String>();

      for(Integer i : expected){
        expected_names.add(symbl_name_from_id(i));
      }

      Lexico.closeFile((s.left + 1), (s.right + 1));

      ComplexSymbol complexSymbol = (ComplexSymbol) s;

      throw new CompilerError(complexSymbol, expected_names, true, filenamePath);
    } catch (Exception e) {
		};
  }

  public void unrecovered_syntax_error(Symbol s){
    try {
      ArrayList<Integer> expected = new ArrayList<Integer>(expected_token_ids());
      ArrayList<String> expected_names = new ArrayList<String>();

      for(Integer i : expected){
        expected_names.add(symbl_name_from_id(i));
      }

      Lexico.closeFile((s.left + 1), (s.right + 1));

      ComplexSymbol complexSymbol = (ComplexSymbol) s;

      throw new CompilerError(complexSymbol, expected_names, false, filenamePath);
    } catch (Exception e) {
      SymTabError.closeFile();
      CompilerError.closeFiles();
		};
  }

  private void initTypes() {
    try{
      //init TUPLE type
      Type tuple_type = new Type(Tipo.dtype, TipoSubyacente.TS_TUPLE, 0);
      symbolsTable.add("tuple", tuple_type);

      //init NUMBER type
      Type number_type = new Type(Tipo.dtype, TipoSubyacente.TS_NUMBER, 16);
      symbolsTable.add("number", number_type);

      // init String type
      Type str_type = new Type(Tipo.dtype, TipoSubyacente.TS_STRING, 16);
      symbolsTable.add("string", str_type);

      //init BOOLEAN type
      Type bool_type = new Type(Tipo.dtype,TipoSubyacente.TS_BOOLEAN, 16);
      symbolsTable.add("boolean",bool_type);

      //init VOID type
      Type void_type = new Type(Tipo.dtype, TipoSubyacente.TS_NULL, 0);
      symbolsTable.add("void", void_type);
    }catch (SymTabError e){
      SymTabError.closeFile();
      CompilerError.closeFiles();
    }
  }

:};

// Terminal Symbols

terminal String ID, NUMBER, STRING, BOOL, STRUCT, OP_RELATIONAL, OP_LOGICAL, OP_LOGICAL_NOT, OP_ARITHMETICAL_C, OP_ARITHMETICAL_B;

terminal        OP_ASSIG, IF, ELIF, ELSE,
                WHILE, FUNC, RETURN, CONSTANT, FOR,
                READ, PRINT, LPAREN, RPAREN, LBRACE, RBRACE, LBRACKET, RBRACKET,
                SEMICOLON, COMMA, TWO_POINTS, MAIN;


// Non-terminal Symbols

non terminal  Start                 STARTS;
non terminal  Init                  INIT;

non terminal  MainLabel             m_main;
non terminal  MainS                 main_s;
non terminal  MainInstructions      main_instructions;

non terminal  Instructions          instructions;
non terminal  Instruction           instruction;

non terminal  BlockIn               block_in;
non terminal  BlockOut              block_out;

non terminal  Declarations          declarations, declaration;

non terminal  Const                 constant;

non terminal  NumericValue          number_value;
non terminal  Identifier            id_value;
non terminal  StringValue           string_value;
non terminal  BooleanValue          boolean_value;

non terminal  Value                 value;

non terminal  Assignation           operator_assig;
non terminal  InstructionOut        instruction_out;

non terminal  ArithmeticOperator    arith_op;
non terminal  ArithmeticOperation   arithmetical_operation;
non terminal  ArithmeticValue       arithmetical_value;

non terminal  BooleanOperator       boolean_operation;
non terminal  BooleanOperationValue boolean_op_value;
non terminal  ValuesComparison      value_comparison;
non terminal  End                   m_end;
non terminal  Condition             cond;
non terminal  CondFalse             cond_f;
non terminal  CondTrue              cond_t;
non terminal  InstructionIf         instruction_if;
non terminal  ElseIf                instruction_elif;
non terminal  Else                  instruction_else;
non terminal  WhileLabel            m_while;
non terminal  ForBody               for_body;
non terminal  ForLabel              label_for;
non terminal  LabelPostFor          label_post_for;
non terminal  InstructionWhile      instruction_while;
non terminal  InstructionFor        instruction_for;
non terminal  DeclareFunctions      declare_functions;
non terminal  DefineFunction        define_function;
non terminal  FunctionInit          function_init;
non terminal  FunctionParams        function_params;
non terminal  FunctionWithParams    function_w_params;
non terminal  FunctionInstructions  function_instructions;
non terminal  FunctionReturn        function_return;

non terminal  CallFunction          call_function;
non terminal  CallBody              call_body;

non terminal  TupleDeclaration      tuple_declaration;
non terminal  FieldList             field_list;
non terminal  TupleField            tuple_field;
non terminal  TupleValues           tuple_value;
non terminal  TupleValues           tuple_values;

non terminal  Empty                 EMPTY;

// precedences
precedence left OP_ARITHMETICAL_B;
precedence left OP_ARITHMETICAL_C;
precedence left OP_LOGICAL;
precedence left OP_LOGICAL_NOT;

start with STARTS;

STARTS                      ::=  INIT declare_functions
                            {:
                            // close SymbolsTableData.txt file
                              symbolsTable.closeSymbolsTableFiles();
                              backTables.guardarTablasBack();

                              c3a_g.guardarC3Dir();
                              assembler.generateAssembler();

                              SymTabError.closeFile();
                              CompilerError.closeFiles();
                            :};

INIT                        ::= {:
                              initTypes();
                              RESULT = new Init();
                            :};

declare_functions           ::= define_function declare_functions
                            {:
                              RESULT = new DeclareFunctions();
                            :}
                            | main_s
                            {:
                              RESULT = new DeclareFunctions();
                            :};

define_function             ::= function_init:init_func LBRACE block_in function_instructions function_return:ret_func block_out RBRACE
                            {:
                              //Check function type and return type
                              if (init_func.getTipoSubyacente() != ret_func.getTipoSubyacente()) {

                                String from = ret_funcxleft.getLine() + ":" + ret_funcxleft.getColumn();
                                String to = ret_funcxright.getLine() + ":" + ret_funcxright.getColumn();
                                String message = "Invalid return type. Expected : "+init_func.getTipoSubyacente()+", but got: "+ret_func.getTipoSubyacente() + " from "+from+" to "+to;

                                throw new CompilerError(message, CompilerError.ErrorType.SEMANTIC, filenamePath);
                              }

                              // NO RETURN
                              if (init_func.getTipoSubyacente() == TipoSubyacente.TS_NULL){
                                c3a_g.generarC3A(Code.rtn,null,null,init_func.getFunId()); 
                              // RETURN
                              } else {
                                c3a_g.generarC3A(Code.rtn,ret_func.getVarId(),null,init_func.getFunId());
                              }

                              RESULT = new DefineFunction();
                            :};

function_init               ::= FUNC ID:fun_id function_params:fun_params RPAREN TWO_POINTS ID:type_id
                            {:
                              try{
                                // check return type
                                Type return_fun_type = symbolsTable.get(type_id);
                                if(return_fun_type.getTipo() != Tipo.dtype){
                                  String from = type_idxleft.getLine() + ":" + type_idxleft.getColumn();
                                  String to = type_idxright.getLine() + ":" + type_idxright.getColumn();
                                  String message = "Function declaration : invalid type" + " from "+from+" to "+to;
                                  throw new CompilerError(message, CompilerError.ErrorType.SEMANTIC, filenamePath);
                                }
                                if(return_fun_type.getTipoSubyacente() == TipoSubyacente.TS_STRING){
                                  String from = type_idxleft.getLine() + ":" + type_idxleft.getColumn();
                                  String to = type_idxright.getLine() + ":" + type_idxright.getColumn();
                                  String message = "Invalid return type : "+type_id+". Valid return types are : boolean or number" + " from "+from+" to "+to;
                                  throw new CompilerError(message, CompilerError.ErrorType.SEMANTIC, filenamePath);
                                }

                                TipoSubyacente subType = return_fun_type.getTipoSubyacente();
                                ArrayList<Parameter> params = fun_params.getParams();
                                String backId = backTables.addProcedure(fun_id, params.size(), 0, subType);

                                Type fun_type = new Type(backId, Tipo.dfun, type_id);

                                symbolsTable.add(fun_id, fun_type);

                                // add params if it has
                                for(Parameter param : params){
                                  Type param_type = param.getType();
                                  TipoSubyacente param_subType = symbolsTable.get(param.getType().getTypeName()).getTipoSubyacente();
                                  String param_backId = backTables.addVariable(param.getVarId(), param_subType, param.getSize(), true);
                                  param_type.setIdBack(param_backId);
                                  symbolsTable.addParam(fun_id, param_backId, param.getVarId(), param_type);
                                }

                                c3a_g.generarC3A(Code.skip,null,null,backId);
                                c3a_g.generarC3A(Code.pmb,null,null,backId);

                                RESULT = new FunctionInit(backId, subType);
                              }catch(SymTabError e){
                                String from = type_idxleft.getLine() + ":" + type_idxleft.getColumn();
                                String to = type_idxright.getLine() + ":" + type_idxright.getColumn();
                                String message = e.getMessage() + " from "+from+" to "+to;
                                throw new CompilerError(message, CompilerError.ErrorType.SEMANTIC, filenamePath);
                              }
                            :};

function_params             ::= function_w_params:params
                            {:
                              RESULT = new FunctionParams(params.getParams());
                            :}
                            | LPAREN
                            {:
                              RESULT = new FunctionParams();
                            :};

function_w_params           ::= function_w_params:prev_params COMMA ID:id TWO_POINTS ID:type_id
                            {:
                              try{
                                //check param type
                                Type type_type = symbolsTable.get(type_id);
                                if(type_type.getTipo() != Tipo.dtype){
                                  String from = idxleft.getLine() + ":" + idxleft.getColumn();
                                  String to = type_idxright.getLine() + ":" + type_idxright.getColumn();
                                  String message = "Invalid param type" + " from "+from+" to "+to;
                                  throw new CompilerError(message, CompilerError.ErrorType.SEMANTIC, filenamePath);
                                }
                                if(type_type.getTipoSubyacente() == TipoSubyacente.TS_STRING){
                                  String from = idxleft.getLine() + ":" + idxleft.getColumn();
                                  String to = type_idxright.getLine() + ":" + type_idxright.getColumn();
                                  String message = "Invalid param type : "+type_id+". Valid params types are : boolean or number" + " from "+from+" to "+to;
                                  throw new CompilerError(message, CompilerError.ErrorType.SEMANTIC, filenamePath);
                                }

                                Type param_type = new Type(Tipo.darg, type_id);

                                Parameter param = new Parameter(id, param_type, type_type.getSize());

                                RESULT = new FunctionWithParams(param, prev_params);
                              }catch(SymTabError e){
                                String from = type_idxleft.getLine() + ":" + type_idxleft.getColumn();
                                String to = type_idxright.getLine() + ":" + type_idxright.getColumn();
                                String message = e.getMessage() + " from "+from+" to "+to;
                                throw new CompilerError(message, CompilerError.ErrorType.SEMANTIC, filenamePath);
                              }
                            :}
                            | LPAREN ID:id TWO_POINTS ID:type_id
                            {:
                              try{
                                //check param type
                                Type type_type = symbolsTable.get(type_id);
                                if(type_type.getTipo() != Tipo.dtype){
                                  String from = idxleft.getLine() + ":" + idxleft.getColumn();
                                  String to = type_idxright.getLine() + ":" + type_idxright.getColumn();
                                  String message = "Invalid param type" + " from "+from+" to "+to;
                                  throw new CompilerError(message, CompilerError.ErrorType.SEMANTIC, filenamePath);
                                }
                                if(type_type.getTipoSubyacente() == TipoSubyacente.TS_STRING){
                                  String from = idxleft.getLine() + ":" + idxleft.getColumn();
                                  String to = type_idxright.getLine() + ":" + type_idxright.getColumn();
                                  String message = "Invalid param type : "+type_id+". Valid params types are : boolean or number" + " from "+from+" to "+to;
                                  throw new CompilerError(message, CompilerError.ErrorType.SEMANTIC, filenamePath);
                                }

                                Type param_type = new Type(Tipo.darg, type_id);

                                Parameter param = new Parameter(id, param_type, type_type.getSize());

                                RESULT = new FunctionWithParams(param);
                              }catch(SymTabError e){
                                String from = type_idxleft.getLine() + ":" + type_idxleft.getColumn();
                                String to = type_idxright.getLine() + ":" + type_idxright.getColumn();
                                String message = e.getMessage() + " from "+from+" to "+to;
                                throw new CompilerError(message, CompilerError.ErrorType.SEMANTIC, filenamePath);
                              }
                            :};

function_instructions       ::= instructions
                            {:
                              RESULT = new FunctionInstructions();
                            :}
                            |
                            {:
                              RESULT = new FunctionInstructions();
                            :};

function_return             ::= RETURN value:value SEMICOLON
                            {:
                              try{
                                // is literall
                                TipoSubyacente subType = null;
                                if(value.getTipo() == Tipo.dnull){
                                  subType = value.getTipoSubyacente();
                                }

                                // is variable
                                if(value.getTipo() != Tipo.dnull){
                                  Type type_type = symbolsTable.get(value.getTypeName());
                                  subType = type_type.getTipoSubyacente();
                                }

                                RESULT = new FunctionReturn(value.getVarId(), subType);
                              }catch(SymTabError e){
                                throw new CompilerError(e.getMessage(), CompilerError.ErrorType.SEMANTIC, filenamePath);
                              }
                            :}
                            | EMPTY
                            {:
                              RESULT = new FunctionReturn();
                            :};

main_s                        ::= FUNC MAIN m_main LPAREN RPAREN LBRACE block_in main_instructions block_out RBRACE {:
                              c3a_g.generarC3A(Code.rtn,null,null,"PROC_main");

                              RESULT = new MainS();
                            :};

m_main                      ::=
                            {:

                              String backId = backTables.addMain();
                              Type fun_type = new Type(backId, Tipo.dfun, null);
                              symbolsTable.add("main", fun_type);

                              c3a_g.generarC3A(Code.skip,null,null,"main");
                              c3a_g.generarC3A(Code.pmb,null,null,backId);

                              RESULT = new MainLabel();
                            :};

main_instructions           ::= instructions
                            {:
                              RESULT = new MainInstructions();
                            :}
                            | EMPTY
                            {:
                              RESULT = new MainInstructions();
                            :};

instructions                ::= instructions instruction
                            {:
                              RESULT = new Instructions();
                            :}
                            | instruction
                            {:
                              RESULT = new Instructions();
                            :};

instruction                 ::=  declarations SEMICOLON
                            {:
                              RESULT = new Instruction();
                            :}
                            | tuple_declaration SEMICOLON
                            {:
                              RESULT = new Instruction();
                            :}
                            | instruction_if
                            {:
                              RESULT = new Instruction();
                            :}
                            | instruction_while
                            {:
                              RESULT = new Instruction();
                            :}
                            | instruction_for
                            {:
                              RESULT = new Instruction();
                            :}
                            | operator_assig SEMICOLON
                            {:
                              RESULT = new Instruction();
                            :}
                            | instruction_out SEMICOLON
                            {:
                              RESULT = new Instruction();
                            :}
                            | call_function SEMICOLON
                            {:
                              RESULT = new Instruction();
                            :};

block_in                    ::=
                            {:
                              symbolsTable.enterBlock();
                              RESULT = new BlockIn();
                            :};

block_out                   ::=
                            {:
                              try{
                                symbolsTable.leaveBlock();
                                RESULT = new BlockOut();
                              }catch(SymTabError e){
                                throw new CompilerError(e.getMessage(), CompilerError.ErrorType.SEMANTIC, filenamePath);
                              }
                            :};

declarations                ::=  ID:var_id COMMA declarations:declarations
                            {:
                              try {
                                Type type = declarations.getType();
                                String backId;

                                if (type.getTipoSubyacente() == TipoSubyacente.TS_STRING) {
                                  String value = declarations.getValue();
                                  backId = backTables.addStringVariable(var_id, type.getSize(), value);
                                } else {
                                  backId = backTables.addVariable(var_id, type.getTipoSubyacente(), type.getSize(), false);
                                }

                                type.setIdBack(backId);
                                symbolsTable.add(var_id, type);
                                c3a_g.generarC3A(Code.copy,declarations.getVarId(),null,backId);

                                RESULT = declarations;

                              } catch(SymTabError e) {
                                String from = var_idxleft.getLine() + ":" + var_idxleft.getColumn();
                                String to = var_idxright.getLine() + ":" + var_idxright.getColumn();
                                String message = e.getMessage() + " from "+from+" to "+to;
                                throw new CompilerError(message, CompilerError.ErrorType.SEMANTIC, filenamePath);
                              }
                            :}
                            | declaration:declaration
                            {:
                              RESULT = declaration;
                            :}
                            ;

tuple_declaration           ::= STRUCT LBRACE field_list:field_list RBRACE ID:var_id
                            {:
                            Type type = symbolsTable.get("tuple");
                            type.setSize(field_list.getSize());
                            type.setTupleTypes(field_list.getTypes());
                            symbolsTable.add(var_id, type);
                            RESULT = new TupleDeclaration(var_id, field_list, field_list.getSize());
                            :}
                            ;

field_list                  ::= field_list:field_list SEMICOLON tuple_field:field
                            {:
                            field_list.addField(field);
                            RESULT = field_list;
                            :}
                            | tuple_field:field
                            {:
                            ArrayList<TupleField> tupleFields = new ArrayList<>();
                            tupleFields.add(field);
                            RESULT = new FieldList(tupleFields);
                            :}
                            ;

tuple_field                 ::= ID:id_var TWO_POINTS ID:type_id
                            {:
                            Type type = symbolsTable.get(type_id);
                            if (type == null || type.getTipo() != Tipo.dtype) {
                              String from = type_idxleft.getLine() + ":" + type_idxleft.getColumn();
                              String to = type_idxright.getLine() + ":" + type_idxright.getColumn();
                              String message = "Invalid type" + " from "+from+" to "+to;
                              throw new CompilerError(message, CompilerError.ErrorType.SEMANTIC, filenamePath);
                            }

                            String backId = backTables.addVariable(id_var, type.getTipoSubyacente(), type.getSize(), false);
                            type.setIdBack(backId);
                            symbolsTable.add(id_var, type);

                            RESULT = new TupleField(id_var, type);
                            :}
                            ;

declaration                 ::=  ID:id_var constant:constant TWO_POINTS ID:type_id OP_ASSIG value:value
                            {:
                              try {

                                boolean isConst = constant != null ? constant.getIsConst() : false;

                                Type type = symbolsTable.get(type_id);

                                if (type == null || type.getTipo() != Tipo.dtype) {
                                  String from = type_idxleft.getLine() + ":" + type_idxleft.getColumn();
                                  String to = type_idxright.getLine() + ":" + type_idxright.getColumn();
                                  String message = "Invalid type" + " from "+from+" to "+to;
                                  throw new CompilerError(message, CompilerError.ErrorType.SEMANTIC, filenamePath);
                                }

                                if (type.getTipoSubyacente() != TipoSubyacente.TS_BOOLEAN
                                  && type.getTipoSubyacente() != TipoSubyacente.TS_NUMBER
                                  && type.getTipoSubyacente() != TipoSubyacente.TS_STRING) {
                                    String from = type_idxleft.getLine() + ":" + type_idxleft.getColumn();
                                    String to = type_idxright.getLine() + ":" + type_idxright.getColumn();
                                    String message = "DECLARATION: Invalid subjacent type" + " from "+from+" to "+to;
                                    throw new CompilerError(message, CompilerError.ErrorType.SEMANTIC, filenamePath);
                                }

                                //IF LITERALL (type = null) CHECK SUBJACENT Tipo
                                if (value.getTipo() == Tipo.dnull && type.getTipoSubyacente() != value.getTipoSubyacente()) {
                                  String from = type_idxleft.getLine() + ":" + type_idxleft.getColumn();
                                  String to = type_idxright.getLine() + ":" + type_idxright.getColumn();
                                  String message = "Value and type must have same subjacent type" + " from "+from+" to "+to;
                                  throw new CompilerError(message, CompilerError.ErrorType.SEMANTIC, filenamePath);
                                }

                                if (value.getTipo() != Tipo.dnull && !value.getTypeName().equals(type_id)) {
                                  String from = type_idxleft.getLine() + ":" + type_idxleft.getColumn();
                                  String to = type_idxright.getLine() + ":" + type_idxright.getColumn();
                                  String message = "Value and type must have same subjacent type" + " from "+from+" to "+to;
                                  throw new CompilerError(message, CompilerError.ErrorType.SEMANTIC, filenamePath);
                                }

                                Type var_type;

                                if(isConst){
                                  var_type = new Type(Tipo.dconst, type_id, value.getValue());
                                }
                                else{
                                  var_type = new Type(Tipo.dvar, type_id);
                                }

                                int size = type.getSize();
                                if(value.getIsString()){
                                  size = value.getStringSize();
                                }

                                String var_id;
                                if(type.getTipoSubyacente() == TipoSubyacente.TS_STRING){
                                  String str_value = (String) value.getValue();
                                  var_id = backTables.addStringVariable(id_var, type.getSize(), str_value);
                                }else{
                                  var_id = backTables.addVariable(id_var, type.getTipoSubyacente(), type.getSize(), false);
                                }

                                var_type.setIdBack(var_id);

                                String val_id = value.getVarId();
                                symbolsTable.add(id_var, var_type);

                                Declarations symDcls;
                                if (value.getIsString()) {
                                  symDcls = new Declarations(val_id, var_type, (String) value.getValue());
                                } else {
                                  symDcls = new Declarations(val_id, var_type);
                                }

                                if (type.getTipoSubyacente() != TipoSubyacente.TS_STRING) {
                                  c3a_g.generarC3A(Code.copy,val_id,null,var_id);
                                }

                                RESULT = symDcls;

                              } catch(SymTabError e) {
                                String from = id_varxleft.getLine() + ":" + id_varxleft.getColumn();
                                String to = id_varxright.getLine() + ":" + id_varxright.getColumn();
                                String message = e.getMessage() + " from "+from+" to "+to;
                                throw new CompilerError(message, CompilerError.ErrorType.SEMANTIC, filenamePath);
                              }
                            :}
                            | ID:id_var constant TWO_POINTS ID:type_id OP_ASSIG tuple_value:tuple_value
                            {:
                            Type type = symbolsTable.get(type_id);

                            if (type == null || type.getTipo() != Tipo.dtype) {
                              String from = type_idxleft.getLine() + ":" + type_idxleft.getColumn();
                              String to = type_idxright.getLine() + ":" + type_idxright.getColumn();
                              String message = "Invalid type" + " from "+from+" to "+to;
                              throw new CompilerError(message, CompilerError.ErrorType.SEMANTIC, filenamePath);
                            }

                            if (type.getTipoSubyacente() != TipoSubyacente.TS_TUPLE) {
                                String from = type_idxleft.getLine() + ":" + type_idxleft.getColumn();
                                String to = type_idxright.getLine() + ":" + type_idxright.getColumn();
                                String message = "DECLARATION: Invalid subjacent type" + " from "+from+" to "+to;
                                throw new CompilerError(message, CompilerError.ErrorType.SEMANTIC, filenamePath);
                            }

                            //COMPROBAMOS QUE LA CANTIDAD DE VALORES COINCIDA CON LA CANTIDAD DE VARIABLES DE LA TUPLA
                            if (type.getTupleTypes().size() != tuple_value.getValues().size()) {
                                String from = type_idxleft.getLine() + ":" + type_idxleft.getColumn();
                                String to = type_idxright.getLine() + ":" + type_idxright.getColumn();
                                String message = "TUPLE_DECLARATION: Invalid number of values" + " from "+from+" to "+to;
                                throw new CompilerError(message, CompilerError.ErrorType.SEMANTIC, filenamePath);
                            }

                            //COMPROBAMOS QUE LOS TIPOS DE LOS VALUES SEAN COMPATIBLES CON LOS TIPOS DE LA TUPLA
                            for (int i = 0; i < tuple_value.getValues().size(); i++) {
                                if(type.getTupleTypes().get(i).getTipoSubyacente() != tuple_value.getValues().get(i).getTipoSubyacente()){
                                    String from = type_idxleft.getLine() + ":" + type_idxleft.getColumn();
                                    String to = type_idxright.getLine() + ":" + type_idxright.getColumn();
                                    String message = "TUPLE_DECLARATION: Invalid subjacentType of value" + " from "+from+" to "+to;
                                    throw new CompilerError(message, CompilerError.ErrorType.SEMANTIC, filenamePath);
                                }
                            }

                            for (int i = 0; i < tuple_value.getValues().size(); i++) {
                                var value = tuple_value.getValues().get(i);
                                var varType = type.getTupleTypes().get(i);
                                c3a_g.generarC3A(Code.copy,value.getVarId(),null,varType.getBackendId());
                            }

                            backTables.addVariable(id_var, type.getTipoSubyacente(), type.getSize(), false);

                            RESULT = new Declarations();

                            :}
                            ;

constant                    ::=  CONSTANT
                            {:
                              RESULT = new Const(true);
                            :}
                            |  EMPTY
                            {:
                              RESULT = new Const(false);
                            :};

operator_assig              ::= ID:var_id OP_ASSIG value:value
                            {:
                              try {
                                   Type varType = symbolsTable.get(var_id);
                                   String type_id = varType.getTypeName();

                                   if (varType.getTipo() == Tipo.dconst) {
                                       String from = var_idxleft.getLine() + ":" + var_idxleft.getColumn();
                                       String to = valuexright.getLine() + ":" + valuexright.getColumn();
                                       String message = "Cant assign value to constant" + " from " + from + " to " + to;
                                       throw new CompilerError(message, CompilerError.ErrorType.SEMANTIC, filenamePath);
                                   }

                                   //IF LITERALL (type = null) CHECK SUBJACENT Tipo
                                   if (value.getTipo() == Tipo.dnull && varType.getTipoSubyacente() == value.getTipoSubyacente()) {
                                       String from = var_idxleft.getLine() + ":" + var_idxleft.getColumn();
                                       String to = valuexright.getLine() + ":" + valuexright.getColumn();
                                       String message = "Value and type must have same subjacent type" + " from " + from + " to " + to;
                                       throw new CompilerError(message, CompilerError.ErrorType.SEMANTIC, filenamePath);
                                   }

                                   if (value.getTipo() != Tipo.dnull && !value.getTypeName().equals(type_id)) {
                                       String from = var_idxleft.getLine() + ":" + var_idxleft.getColumn();
                                       String to = valuexright.getLine() + ":" + valuexright.getColumn();
                                       String message = "ValueType and type_id must be the same type" + " from " + from + " to " + to;
                                       throw new CompilerError(message, CompilerError.ErrorType.SEMANTIC, filenamePath);
                                   }

                                   c3a_g.generarC3A(Code.copy,value.getVarId(),null,varType.getBackendId());

                                   RESULT = new Assignation();
                              } catch (SymTabError e) {
                                  String from = var_idxleft.getLine() + ":" + var_idxleft.getColumn();
                                  String to = var_idxright.getLine() + ":" + var_idxright.getColumn();
                                  String message = e.getMessage() + " from " + from + " to " + to;
                                  throw new CompilerError(message, CompilerError.ErrorType.SEMANTIC, filenamePath);
                              }
                            :};

arith_op                    ::= OP_ARITHMETICAL_B:op
                            {: RESULT = new ArithmeticOperator(op); :}
                            | OP_ARITHMETICAL_C:op
                            {: RESULT = new ArithmeticOperator(op); :};

arithmetical_operation      ::= arithmetical_value:arith_val1 arith_op:op arithmetical_value:arith_val2
                            {:
                              try {
                                ArithmeticOperation sym;
                                boolean allConst = arith_val1.getIsConst() && arith_val2.getIsConst();

                                Code op_code;

                                switch (op.getOperator()) {
                                  case "+" :
                                    op_code = Code.add;
                                    break;
                                  case "-" :
                                    op_code = Code.sub;
                                    break;
                                  case "*" :
                                    op_code = Code.prod;
                                    break;
                                  case "/" :
                                    op_code = Code.div;
                                    break;
                                  case "%" :
                                    op_code = Code.mod;
                                    break;
                                  default :
                                    String from = opxleft.getLine() + ":" + opxleft.getColumn();
                                    String to = opxright.getLine() + ":" + opxright.getColumn();
                                    String message = "Invalid arithmetic operator from "+from+" to "+to;
                                    throw new CompilerError(message, CompilerError.ErrorType.SEMANTIC, filenamePath);
                                }

                                Integer value = null;
                                if (allConst) {
                                  Integer val1 = (Integer) arith_val1.getValue();
                                  Integer val2 = (Integer) arith_val2.getValue();

                                  switch(op.getOperator()){
                                    case "+" :
                                      value = val1 + val2;
                                      break;
                                    case "-" :
                                      value = val1 - val2;
                                      break;
                                    case "*" :
                                      value = val1 * val2;
                                      break;
                                    case "/" :
                                      if(val2 == 0){
                                        String from = arith_val2xleft.getLine() + ":" + arith_val2xleft.getColumn();
                                        String to = arith_val2xright.getLine() + ":" + arith_val2xright.getColumn();
                                        String message = "Can not divide by zero (0)" + " from "+from+" to "+to;
                                        throw new CompilerError(message, CompilerError.ErrorType.SEMANTIC, filenamePath);
                                      }
                                      value = val1 / val2;
                                      break;
                                    case "%" :
                                      value = val1 % val2;
                                      break;
                                  }
                                }

                                Type type_number = symbolsTable.get("number");

                                String var_id = backTables.addTempVar(type_number.getSize(), type_number.getTipoSubyacente());

                                c3a_g.generarC3A(op_code,arith_val1.getVarId(),arith_val2.getVarId(),var_id);

                                if(allConst){
                                  sym = new ArithmeticOperation(var_id, value);
                                }
                                else{
                                  sym = new ArithmeticOperation(var_id);
                                }

                                RESULT = sym;
                              }catch(SymTabError e){
                                throw new CompilerError(e.getMessage(), CompilerError.ErrorType.SEMANTIC, filenamePath);
                              }
                            :}
                            | OP_ARITHMETICAL_B:op LPAREN arithmetical_operation:arith_op RPAREN
                            {:
                              try{
                                ArithmeticOperation sym = arith_op;
                                if(op.equals("-")){
                                  Type type_number = symbolsTable.get("number");
                                  String var_id = backTables.addTempVar(type_number.getSize(),  type_number.getTipoSubyacente());

                                  if(arith_op.getIsConst()){
                                    Integer value = - arith_op.getValue().intValue();
                                    sym = new ArithmeticOperation(arith_op.getVarId(), value);
                                  }else{
                                    sym = new ArithmeticOperation(var_id);
                                  }

                                  c3a_g.generarC3A(Code.neg,arith_op.getVarId(),null,var_id);
                                }

                                RESULT = sym;
                              }catch(SymTabError e){
                                throw new CompilerError(e.getMessage(), CompilerError.ErrorType.SEMANTIC, filenamePath);
                              }
                            :}
                            | LPAREN arithmetical_operation:arith_op RPAREN
                            {:
                              RESULT = arith_op;
                            :};

number_value                ::= NUMBER:n_val
                            {:
                              Integer value = Integer.valueOf(n_val);
                              RESULT = new NumericValue(value);
                            :}
                            | LPAREN number_value:n_val RPAREN
                            {:
                              RESULT = n_val;
                            :}
                            | OP_ARITHMETICAL_B:op number_value:n_val
                            {:
                              NumericValue sym = n_val;
                              if(op.equals("-")){
                                Integer value = - n_val.getValue().intValue();
                                sym = new NumericValue(value);
                              }

                              RESULT = sym;
                            :};

arithmetical_value         ::= number_value:n_val
                          {:
                            try{
                              Integer value = n_val.getValue();

                              Type type_number = symbolsTable.get("number");

                              String var_id = backTables.addTempVar(type_number.getSize(),  type_number.getTipoSubyacente());

                              c3a_g.generarC3A(Code.copy,Integer.toString(value),null,var_id);

                              RESULT = new ArithmeticValue(var_id, value);
                            }catch(SymTabError e){
                              throw new CompilerError(e.getMessage(), CompilerError.ErrorType.SEMANTIC, filenamePath);
                            }
                          :}
                          | id_value:id_val
                          {:
                            try{
                              Tipo main_var_type = id_val.getTipo();

                              if(main_var_type != Tipo.darg && main_var_type != Tipo.dconst && main_var_type != Tipo.dvar){
                                String from = id_valxleft.getLine() + ":" + id_valxleft.getColumn();
                                String to = id_valxright.getLine() + ":" + id_valxright.getColumn();
                                String message = "Invalid arithmetic value type" + " from "+from+" to "+to;
                                throw new CompilerError(message, CompilerError.ErrorType.SEMANTIC, filenamePath);
                              }

                              Type var_dtype = symbolsTable.get(id_val.getTypeName());

                              if(var_dtype.getTipoSubyacente() != TipoSubyacente.TS_NUMBER){
                                String from = id_valxleft.getLine() + ":" + id_valxleft.getColumn();
                                String to = id_valxright.getLine() + ":" + id_valxright.getColumn();
                                String message = "ARITHMETICAL_VALUE: Invalid subjacent type" + " from "+from+" to "+to;
                                throw new CompilerError(message, CompilerError.ErrorType.SEMANTIC, filenamePath);
                              }

                              ArithmeticValue sym;
                              String var_id = id_val.getId();

                              if(main_var_type == Tipo.dconst){
                                Integer value = (Integer) id_val.getValue();
                                sym = new ArithmeticValue(var_id, value);
                              }else{
                                sym = new ArithmeticValue(var_id);
                              }

                              RESULT = sym;
                            }catch(SymTabError e){
                              throw new CompilerError(e.getMessage(), CompilerError.ErrorType.SEMANTIC, filenamePath);
                            }
                          :}
                          | arithmetical_operation:arith_op
                          {:
                            ArithmeticValue sym;
                            if(arith_op.getIsConst()){
                              sym = new ArithmeticValue(arith_op.getVarId(), arith_op.getValue());
                            }else{
                              sym = new ArithmeticValue(arith_op.getVarId());
                            }

                            RESULT = sym;
                          :}
                          | call_function:call_fn
                          {:
                            try{
                              Type fn_type = symbolsTable.get(call_fn.getFunctionId());
                              Type dfn_type = symbolsTable.get(fn_type.getTypeName());

                              if(dfn_type.getTipoSubyacente() != TipoSubyacente.TS_NUMBER){
                                String from = call_fnxleft.getLine() + ":" + call_fnxleft.getColumn();
                                String to = call_fnxright.getLine() + ":" + call_fnxright.getColumn();
                                String message = "Invalid function call "+call_fn.getFunctionId() + " with return type "+ dfn_type.getTipoSubyacente() +". Expected :"+TipoSubyacente.TS_NUMBER + " from "+from+" to "+to;
                                throw new CompilerError(message, CompilerError.ErrorType.SEMANTIC, filenamePath);
                              }

                              String var_id = backTables.addTempVar(dfn_type.getSize(), dfn_type.getTipoSubyacente());

                              c3a_g.generarC3A(Code.copy,"return",call_fn.getBackendId(),var_id);

                              RESULT = new ArithmeticValue(var_id);
                            }catch(SymTabError e){
                              String from = call_fnxleft.getLine() + ":" + call_fnxleft.getColumn();
                              String to = call_fnxright.getLine() + ":" + call_fnxright.getColumn();
                              String message = e.getMessage() + " from "+from+" to "+to;
                              throw new CompilerError(message, CompilerError.ErrorType.SEMANTIC, filenamePath);
                            }
                          :}
                          | OP_ARITHMETICAL_B:op call_function:call_fn
                          {:
                            try{
                              Type fn_type = symbolsTable.get(call_fn.getFunctionId());
                              Type dfn_type = symbolsTable.get(fn_type.getTypeName());

                              if(dfn_type.getTipoSubyacente() != TipoSubyacente.TS_NUMBER){
                                String from = call_fnxleft.getLine() + ":" + call_fnxleft.getColumn();
                                String to = call_fnxright.getLine() + ":" + call_fnxright.getColumn();
                                String message = "Invalid function call "+call_fn.getFunctionId() + " with return type "+ dfn_type.getTipoSubyacente() +". Expected :"+TipoSubyacente.TS_NUMBER + " from "+from+" to "+to;
                                throw new CompilerError(message, CompilerError.ErrorType.SEMANTIC, filenamePath);
                              }

                              String var_id = backTables.addTempVar(dfn_type.getSize(), dfn_type.getTipoSubyacente());

                              c3a_g.generarC3A(Code.copy,"return",call_fn.getBackendId(),var_id);

                              if(op.equals("-")){
                                c3a_g.generarC3A(Code.neg,var_id,null,var_id);
                              }

                              RESULT = new ArithmeticValue(var_id);
                            }catch(SymTabError e){
                              String from = call_fnxleft.getLine() + ":" + call_fnxleft.getColumn();
                              String to = call_fnxright.getLine() + ":" + call_fnxright.getColumn();
                              String message = e.getMessage() + " from "+from+" to "+to;
                              throw new CompilerError(message, CompilerError.ErrorType.SEMANTIC, filenamePath);
                            }
                          :};

id_value                    ::= ID:id_val
                            {:
                              try{
                                Type id_type = symbolsTable.get(id_val);
                                String var_id = id_type.getBackendId();
                                Identifier sym = new Identifier(var_id, id_type.getTipo(), id_type.getTypeName());
                                if(id_type.getTipo() == Tipo.dconst){
                                  sym = new Identifier(var_id, id_type.getTipo(), id_type.getTypeName(), id_type.getValue());
                                }

                                RESULT = sym;
                              }catch(SymTabError e){
                                String from = id_valxleft.getLine() + ":" + id_valxleft.getColumn();
                                String to = id_valxright.getLine() + ":" + id_valxright.getColumn();
                                String message = e.getMessage() + " from "+from+" to "+to;
                                throw new CompilerError(message, CompilerError.ErrorType.SEMANTIC, filenamePath);
                              }
                            :}
                            | LPAREN id_value:id_val RPAREN
                            {:
                              RESULT = id_val;
                            :}
                            | OP_LOGICAL_NOT:op id_value:id_val
                            {:
                              try{

                                Identifier sym;
                                // check if unary operator is type valid
                                TipoSubyacente subType = symbolsTable.get(id_val.getTypeName()).getTipoSubyacente();

                                if(subType != TipoSubyacente.TS_BOOLEAN) {
                                  String from = opxleft.getLine() + ":" + opxleft.getColumn();
                                  String to = opxright.getLine() + ":" + opxright.getColumn();
                                  String message = "Invalid unary operator " + op + " for type " + subType + " from "+from+" to "+to;
                                  throw new CompilerError(message, CompilerError.ErrorType.SEMANTIC, filenamePath);
                                }

                                Type type_boolean = symbolsTable.get("boolean");
                                String var_id = backTables.addTempVar(type_boolean.getSize(),  type_boolean.getTipoSubyacente());

                                c3a_g.generarC3A(Code.not,id_val.getId(),null,var_id);

                                if(id_val.getTipo() == Tipo.dconst){
                                  Boolean value = !((Boolean) id_val.getValue()).booleanValue();
                                  sym = new Identifier(var_id, id_val.getTipo(), id_val.getTypeName(), value);
                                }else{
                                  sym = new Identifier(var_id, id_val.getTipo(), id_val.getTypeName());
                                }

                                RESULT = sym;
                              }catch(SymTabError e){
                                throw new CompilerError(e.getMessage(), CompilerError.ErrorType.SEMANTIC, filenamePath);
                              }
                            :}
                            | OP_ARITHMETICAL_B:op id_value:id_val
                            {:
                              try {
                                Identifier sym = id_val;
                                // check if unary operator is type valid
                                TipoSubyacente subType = symbolsTable.get(id_val.getTypeName()).getTipoSubyacente();

                                if(subType != TipoSubyacente.TS_BOOLEAN) {
                                  String from = opxleft.getLine() + ":" + opxleft.getColumn();
                                  String to = opxright.getLine() + ":" + opxright.getColumn();
                                  String message = "Invalid unary operator " + op + " for type " + subType + " from "+from+" to "+to;
                                  throw new CompilerError(message, CompilerError.ErrorType.SEMANTIC, filenamePath);
                                }

                                if(op.equals("-")){
                                  Type type_number = symbolsTable.get("number");
                                  String var_id = backTables.addTempVar(type_number.getSize(),  type_number.getTipoSubyacente());

                                  c3a_g.generarC3A(Code.neg,id_val.getId(),null,var_id);

                                  if(id_val.getTipo() == Tipo.dconst){
                                    Integer value = -((Integer) id_val.getValue()).intValue();
                                    sym = new Identifier(var_id, id_val.getTipo(), id_val.getTypeName(), value);
                                  }else{
                                    sym = new Identifier(var_id, id_val.getTipo(), id_val.getTypeName());
                                  }
                                }

                                RESULT = sym;
                              }catch(SymTabError e){
                                throw new CompilerError(e.getMessage(), CompilerError.ErrorType.SEMANTIC, filenamePath);
                              }
                            :};

string_value                ::= STRING:string_val
                            {:
                              RESULT = new StringValue(string_val);
                            :}
                            | LPAREN string_value:string_val RPAREN
                            {:
                              RESULT = string_val;
                            :};

value                       ::= id_value:id_val
                            {:
                              Value sym;

                              String var_id = id_val.getId();

                              if(id_val.getTipo() == Tipo.dconst){
                                sym = new Value(var_id, id_val.getTipo(), id_val.getTypeName(), id_val.getValue());
                              }
                              else {
                                sym = new Value(var_id, id_val.getTipo(), id_val.getTypeName());
                              }

                              RESULT = sym;
                            :}
                            | string_value:str_value
                            {:
                              try{
                                String value = str_value.getString().replace("\"", "");
                                Type type_string = symbolsTable.get("string");

                                if (value.length() > 1000){
                                  String from = str_valuexleft.getLine() + ":" + str_valuexleft.getColumn();
                                  String to = str_valuexright.getLine() + ":" + str_valuexright.getColumn();
                                  String message = "String must be shorter than 1000 characters " + " from "+from+" to "+to;
                                  throw new CompilerError(message, CompilerError.ErrorType.SEMANTIC, filenamePath);
                                }

                                // string size in bytes
                                int stringSize = value.length() * type_string.getSize();

                                Value sym = new Value(value, stringSize);

                                RESULT = sym;
                              }catch(SymTabError e){
                                throw new CompilerError(e.getMessage(), CompilerError.ErrorType.SEMANTIC, filenamePath);
                              }
                            :}
                            | boolean_value:bool_val
                            {:
                              try{
                                Boolean value = bool_val.getValue();
                                Type type_bool = symbolsTable.get("boolean");

                                String var_id = backTables.addTempVar(type_bool.getSize(), type_bool.getTipoSubyacente());

                                c3a_g.generarC3A(Code.copy,value.toString(),null,var_id);

                                RESULT = new Value(var_id, TipoSubyacente.TS_BOOLEAN, value);
                              }catch(SymTabError e){
                                throw new CompilerError(e.getMessage(), CompilerError.ErrorType.SEMANTIC, filenamePath);
                              }
                            :}
                            | number_value:n_val
                            {:
                              try{
                                Integer value = n_val.getValue();

                                Type type_number = symbolsTable.get("number");

                                String var_id = backTables.addTempVar(type_number.getSize(), type_number.getTipoSubyacente());

                                c3a_g.generarC3A(Code.copy,Integer.toString(value),null,var_id);

                                RESULT = new Value(var_id, TipoSubyacente.TS_NUMBER, value);
                              }catch(SymTabError e){
                                throw new CompilerError(e.getMessage(), CompilerError.ErrorType.SEMANTIC, filenamePath);
                              }
                            :}
                            | value_comparison:comp_val
                            {:
                              Value sym;
                              String id = comp_val.getVarId();

                              if(comp_val.getIsConst()){
                                Boolean value = comp_val.getValue();
                                sym = new Value(id, TipoSubyacente.TS_BOOLEAN, value);
                              }else{
                                sym = new Value(id, TipoSubyacente.TS_BOOLEAN);
                              }

                              RESULT = sym;
                            :}
                            | OP_LOGICAL_NOT value_comparison:comp_val
                            {:
                              try{
                                Value sym;

                                Type type_bool = symbolsTable.get("boolean");
                                String var_id = backTables.addTempVar(type_bool.getSize(), type_bool.getTipoSubyacente());

                                c3a_g.generarC3A(Code.not,comp_val.getVarId(),null,var_id);

                                if(comp_val.getIsConst()){
                                  Boolean value = ! comp_val.getValue().booleanValue();
                                  sym = new Value(var_id, TipoSubyacente.TS_BOOLEAN, value);
                                }else{
                                  sym = new Value(var_id, TipoSubyacente.TS_BOOLEAN);
                                }

                                RESULT = sym;
                              }catch(SymTabError e){
                                throw new CompilerError(e.getMessage(), CompilerError.ErrorType.SEMANTIC, filenamePath);
                              }
                            :}
                            | arithmetical_operation:arith_op
                            {:
                              Value sym;
                              String var_id = arith_op.getVarId();
                              if(arith_op.getIsConst()){
                                sym = new Value(var_id, TipoSubyacente.TS_NUMBER, arith_op.getValue());
                              }else{
                                sym = new Value(var_id, TipoSubyacente.TS_NUMBER);
                              }

                              RESULT = sym;
                            :}
                            | boolean_operation:bool_op
                            {:
                              Value sym;
                              String var_id = new String(bool_op.getVarId());
                              if(bool_op.getIsConst()){
                                sym = new Value(var_id, TipoSubyacente.TS_BOOLEAN, bool_op.getValue());
                              }else{
                                sym = new Value(var_id, TipoSubyacente.TS_BOOLEAN);
                              }

                              RESULT = sym;
                            :}
                            | call_function:call_fn
                            {:
                              try{
                                Type fn_type = symbolsTable.get(call_fn.getFunctionId());
                                Type dfn_type = symbolsTable.get(fn_type.getTypeName());

                                if(dfn_type.getTipoSubyacente() == TipoSubyacente.TS_NULL){
                                  String from = call_fnxleft.getLine() + ":" +call_fnxleft.getColumn();
                                  String to = call_fnxright.getLine() + ":" + call_fnxright.getColumn();
                                  String message = "Function call : "+call_fn.getFunctionId() + " does not return any value" + " from "+from+" to "+to;
                                  throw new CompilerError(message, CompilerError.ErrorType.SEMANTIC, filenamePath);
                                }

                                String var_id = backTables.addTempVar(dfn_type.getSize(), dfn_type.getTipoSubyacente());

                                c3a_g.generarC3A(Code.copy,"return",call_fn.getBackendId(),var_id);

                                RESULT = new Value(var_id, Tipo.dfun, fn_type.getTypeName());
                              }catch(SymTabError e){
                                throw new CompilerError(e.getMessage(), CompilerError.ErrorType.SEMANTIC, filenamePath);
                              }
                            :}
                            | READ LPAREN RPAREN
                            {:
                              try{
                                Type number_type = symbolsTable.get("number");
                                String var_id = backTables.addTempVar(number_type.getSize(), number_type.getTipoSubyacente());

                                c3a_g.generarC3A(Code.read,null,null,var_id);

                                RESULT = new Value(var_id, number_type.getTipoSubyacente());
                              }catch(SymTabError e){
                                throw new CompilerError(e.getMessage(), CompilerError.ErrorType.SEMANTIC, filenamePath);
                              }
                            :}
                            ;

tuple_value                 ::= LBRACKET tuple_values:tuple_values RBRACKET
                            {:
                            RESULT = tuple_values;
                            :}
                            ;

tuple_values                ::=  tuple_values:values COMMA value:value
                            {:
                            values.addValue(value);
                            RESULT = values;
                            :}
                            |   value:value
                            {:
                            RESULT = new TupleValues(new ArrayList<>(List.of(value)));
                            :}
                            ;



boolean_operation           ::= boolean_op_value:bool_val1 OP_LOGICAL:op boolean_op_value:bool_val2
                            {:
                              try{
                                BooleanOperator sym;
                                Code op_code;

                                switch(op){
                                  case "&&":
                                    op_code = Code.and;
                                    break;
                                  case "||":
                                    op_code = Code.or;
                                    break;
                                  default :
                                    String from = opxleft.getLine() + ":" + opxleft.getColumn();
                                    String to = opxright.getLine() + ":" + opxright.getColumn();
                                    String message = "Invalid boolean operator" + " from "+from+" to "+to;
                                    throw new CompilerError(message, CompilerError.ErrorType.SEMANTIC, filenamePath);
                                }

                                boolean allConst = bool_val1.getIsConst() && bool_val2.getIsConst();
                                Boolean value = null;

                                if(allConst){
                                  Boolean val1 = (Boolean) bool_val1.getValue();
                                  Boolean val2 = (Boolean) bool_val2.getValue();
                                  switch(op){
                                  case "&&":
                                    value = val1 && val2;
                                    break;
                                  case "||":
                                    value = val1 || val2;
                                    break;
                                  }
                                }

                                Type type_bool = symbolsTable.get("boolean");
                                String var_id = backTables.addTempVar(type_bool.getSize(), type_bool.getTipoSubyacente());

                                String val1_id = bool_val1.getVarId();
                                String val2_id = bool_val2.getVarId();
                                c3a_g.generarC3A(op_code,val1_id,val2_id,var_id);

                                if(allConst){
                                  sym = new BooleanOperator(var_id, value);
                                }else{
                                  sym = new BooleanOperator(var_id);
                                }

                                RESULT = sym;
                              }catch(SymTabError e){
                                throw new CompilerError(e.getMessage(), CompilerError.ErrorType.SEMANTIC, filenamePath);
                              }
                            :}
                            | LPAREN boolean_operation:bool_op RPAREN
                            {:
                              RESULT = bool_op;
                            :}
                            | OP_LOGICAL_NOT LPAREN boolean_operation:bool_op RPAREN
                            {:
                              try{
                                BooleanOperator sym;
                                Type type_boolean = symbolsTable.get("boolean");
                                String var_id = backTables.addTempVar(type_boolean.getSize(),  type_boolean.getTipoSubyacente());

                                c3a_g.generarC3A(Code.neg,bool_op.getVarId(),null,var_id);

                                if(bool_op.getIsConst()){
                                  Boolean value = ! bool_op.getValue().booleanValue();
                                  sym = new BooleanOperator(bool_op.getVarId(), value);
                                }else{
                                  sym = new BooleanOperator(var_id);
                                }

                                RESULT = sym;
                              }catch(SymTabError e){
                                throw new CompilerError(e.getMessage(), CompilerError.ErrorType.SEMANTIC, filenamePath);
                              }
                            :};

boolean_op_value            ::= boolean_value:bool_val
                            {:
                              try{
                                Boolean value = bool_val.getValue();
                                Type type_boolean = symbolsTable.get("boolean");

                                String var_id = backTables.addTempVar(type_boolean.getSize(),  type_boolean.getTipoSubyacente());

                                c3a_g.generarC3A(Code.copy,value.toString(),null,var_id);

                                RESULT = new BooleanOperationValue(var_id, value);
                              }catch(SymTabError e){
                                throw new CompilerError(e.getMessage(), CompilerError.ErrorType.SEMANTIC, filenamePath);
                              }
                            :}
                            | id_value:id_val
                            {:
                              try{
                                Tipo main_var_type = id_val.getTipo();
                                if(main_var_type != Tipo.darg && main_var_type != Tipo.dconst && main_var_type != Tipo.dvar){
                                  String from = id_valxleft.getLine() + ":" + id_valxleft.getColumn();
                                  String to = id_valxright.getLine() + ":" + id_valxright.getColumn();
                                  String message = "Invalid arithmetic value type" + " from "+from+" to "+to;
                                  throw new CompilerError(message, CompilerError.ErrorType.SEMANTIC, filenamePath);
                                }

                                Type var_dtype = symbolsTable.get(id_val.getTypeName());

                                if(var_dtype.getTipoSubyacente() != TipoSubyacente.TS_BOOLEAN){
                                  String from = id_valxleft.getLine() + ":" + id_valxleft.getColumn();
                                  String to = id_valxright.getLine() + ":" + id_valxright.getColumn();
                                  String message = "boolean_op_value: Invalid subjacent type" + " from "+from+" to "+to;
                                  throw new CompilerError(message, CompilerError.ErrorType.SEMANTIC, filenamePath);
                                }

                                BooleanOperationValue sym;
                                String var_id = id_val.getId();

                                if(main_var_type == Tipo.dconst){
                                  Boolean value = (Boolean) id_val.getValue();
                                  sym = new BooleanOperationValue(var_id, value);
                                }else{
                                  sym = new BooleanOperationValue(var_id);
                                }

                                RESULT = sym;
                              }catch(SymTabError e){
                                throw new CompilerError(e.getMessage(), CompilerError.ErrorType.SEMANTIC, filenamePath);
                              }
                            :}
                            | boolean_operation:bool_op
                            {:
                              BooleanOperationValue sym;
                              if(bool_op.getIsConst()){
                                sym = new BooleanOperationValue(bool_op.getVarId(), bool_op.getValue());
                              }else{
                                sym = new BooleanOperationValue(bool_op.getVarId());
                              }
                              RESULT = sym;
                            :}
                            | value_comparison:comp_val
                            {:
                              BooleanOperationValue sym;
                              String id = new String(comp_val.getVarId());
                              if(comp_val.getIsConst()){
                                sym = new BooleanOperationValue(id, comp_val.getValue());
                              }else{
                                sym = new BooleanOperationValue(id);
                              }

                              RESULT = sym;
                            :}
                            | OP_LOGICAL_NOT value_comparison:comp_val
                            {:
                              try{
                                Type type_bool = symbolsTable.get("boolean");
                                String var_id = backTables.addTempVar(type_bool.getSize(), type_bool.getTipoSubyacente());

                                c3a_g.generarC3A(Code.not,comp_val.getVarId(),null,var_id);

                                BooleanOperationValue sym;
                                if(comp_val.getIsConst()){
                                  Boolean value = ! comp_val.getValue().booleanValue();
                                  sym = new BooleanOperationValue(var_id, value);
                                }else{
                                  sym = new BooleanOperationValue(var_id);
                                }
                                RESULT = sym;
                              }catch(SymTabError e){
                                throw new CompilerError(e.getMessage(), CompilerError.ErrorType.SEMANTIC, filenamePath);
                              }
                            :}
                            | call_function:call_fn
                            {:
                              try{
                                Type fn_type = symbolsTable.get(call_fn.getFunctionId());
                                Type dfn_type = symbolsTable.get(fn_type.getTypeName());

                                if(dfn_type.getTipoSubyacente() != TipoSubyacente.TS_BOOLEAN){
                                  String from = call_fnxleft.getLine() + ":" + call_fnxleft.getColumn();
                                  String to = call_fnxright.getLine() + ":" + call_fnxright.getColumn();
                                  String message = "Invalid function call "+call_fn.getFunctionId() + " with return type "+ dfn_type.getTipoSubyacente() +". Expected :"+TipoSubyacente.TS_BOOLEAN + " from "+from+" to "+to;
                                  throw new CompilerError(message, CompilerError.ErrorType.SEMANTIC, filenamePath);
                                }

                                String var_id = backTables.addTempVar(dfn_type.getSize(), dfn_type.getTipoSubyacente());

                                c3a_g.generarC3A(Code.copy,"return",call_fn.getBackendId(),var_id);

                                RESULT = new BooleanOperationValue(var_id);
                              }catch(SymTabError e){
                                throw new CompilerError(e.getMessage(), CompilerError.ErrorType.SEMANTIC, filenamePath);
                              }
                            :}
                            | OP_LOGICAL_NOT:op call_function:call_fn
                            {:
                              try{
                                Type fn_type = symbolsTable.get(call_fn.getFunctionId());
                                Type dfn_type = symbolsTable.get(fn_type.getTypeName());

                                if(dfn_type.getTipoSubyacente() != TipoSubyacente.TS_NUMBER){
                                  String from = call_fnxleft.getLine() + ":" + call_fnxleft.getColumn();
                                  String to = call_fnxright.getLine() + ":" + call_fnxright.getColumn();
                                  String message = "Invalid function call "+call_fn.getFunctionId() + " with return type "+ dfn_type.getTipoSubyacente() +". Expected :"+TipoSubyacente.TS_NUMBER + " from "+from+" to "+to;
                                  throw new CompilerError(message, CompilerError.ErrorType.SEMANTIC, filenamePath);
                                }

                                String var_id = backTables.addTempVar(dfn_type.getSize(), dfn_type.getTipoSubyacente());

                                c3a_g.generarC3A(Code.copy,"return",call_fn.getBackendId(),var_id);

                                c3a_g.generarC3A(Code.neg,var_id,null,var_id);

                                RESULT = new BooleanOperationValue(var_id);
                              }catch(SymTabError e){
                                throw new CompilerError(e.getMessage(), CompilerError.ErrorType.SEMANTIC, filenamePath);
                              }
                            :}
                            ;

boolean_value               ::= BOOL:bool_val
                            {:
                              Boolean value = Boolean.valueOf(bool_val);
                              RESULT = new BooleanValue(value);
                            :}
                            | OP_LOGICAL_NOT boolean_value:bool_val
                            {:
                              Boolean value = ! bool_val.getValue().booleanValue();
                              RESULT = new BooleanValue(value);
                            :}
                            | LPAREN boolean_value:bool_val RPAREN
                            {:
                              RESULT = bool_val;
                            :}
                            ;

value_comparison            ::= LPAREN value:val1 OP_RELATIONAL:op value:val2 RPAREN
                            {:
                              try{
                                ValuesComparison sym;

                                Tipo type1 = val1.getTipo();
                                TipoSubyacente subType1;

                                if(val1.getTipo() == Tipo.dnull){
                                  subType1 = val1.getTipoSubyacente();
                                }else{
                                  if(type1 != Tipo.dvar && type1 != Tipo.dconst && type1 != Tipo.darg && type1 != Tipo.dfun) {
                                    String from = val1xleft.getLine() + ":" + val1xleft.getColumn();
                                    String to = val1xright.getLine() + ":" + val1xright.getColumn();
                                    String message = "Value 1 of comparison wrong type" + " from "+from+" to "+to;
                                    throw new CompilerError(message, CompilerError.ErrorType.SEMANTIC, filenamePath);
                                  }
                                  Type dtype_type = symbolsTable.get(val1.getTypeName());
                                  subType1 = dtype_type.getTipoSubyacente();
                                }
                                // SAME FOR VALUE2
                                Tipo type2 = val2.getTipo();
                                TipoSubyacente subType2;

                                if(val2.getTipo() == Tipo.dnull){
                                  subType2 = val2.getTipoSubyacente();
                                }else{
                                  if(type2 != Tipo.dvar && type2 != Tipo.dconst && type2 != Tipo.darg && type2 != Tipo.dfun) {
                                    String from = val2xleft.getLine() + ":" + val2xleft.getColumn();
                                    String to = val2xright.getLine() + ":" + val2xright.getColumn();
                                    String message = "Value 2 of comparison wrong type" + " from "+from+" to "+to;
                                    throw new CompilerError(message, CompilerError.ErrorType.SEMANTIC, filenamePath);
                                  }
                                  Type dtype_type = symbolsTable.get(val2.getTypeName());
                                  subType2 = dtype_type.getTipoSubyacente();
                                }

                                //Check same subtype
                                if(subType1 != subType2) {
                                  String from = val2xleft.getLine() + ":" + val2xleft.getColumn();
                                  String to = val2xright.getLine() + ":" + val2xright.getColumn();
                                  String message = "Value 1 subtype is different from value 2" + " from "+from+" to "+to;
                                  throw new CompilerError(message, CompilerError.ErrorType.SEMANTIC, filenamePath);
                                }

                                Code op_code = Code.EQ;
                                switch(op){
                                  case "==":
                                    op_code = Code.EQ;
                                    break;
                                  case "!=":
                                    op_code = Code.NE;
                                    break;
                                  case "<":
                                    if(subType1 != TipoSubyacente.TS_NUMBER) {
                                      String from = opxleft.getLine() + ":" + opxleft.getColumn();
                                      String to = opxright.getLine() + ":" + opxright.getColumn();
                                      String message = "This operator ("+op+") only works with integer subtypes" + " from "+from+" to "+to;
                                      throw new CompilerError(message, CompilerError.ErrorType.SEMANTIC, filenamePath);
                                    }
                                    op_code = Code.LT;
                                    break;
                                  case "<=":
                                    if(subType1 != TipoSubyacente.TS_NUMBER) {
                                      String from = opxleft.getLine() + ":" + opxleft.getColumn();
                                      String to = opxright.getLine() + ":" + opxright.getColumn();
                                      String message = "This operator ("+op+") only works with integer subtypes" + " from "+from+" to "+to;
                                      throw new CompilerError(message, CompilerError.ErrorType.SEMANTIC, filenamePath);
                                    }
                                    op_code = Code.LE;
                                    break;
                                  case ">":
                                    if(subType1 != TipoSubyacente.TS_NUMBER) {
                                      String from = opxleft.getLine() + ":" + opxleft.getColumn();
                                      String to = opxright.getLine() + ":" + opxright.getColumn();
                                      String message = "This operator ("+op+") only works with integer subtypes" + " from "+from+" to "+to;
                                      throw new CompilerError(message, CompilerError.ErrorType.SEMANTIC, filenamePath);
                                    }
                                    op_code = Code.GT;
                                    break;
                                  case ">=":
                                    if(subType1 != TipoSubyacente.TS_NUMBER) {
                                      String from = opxleft.getLine() + ":" + opxleft.getColumn();
                                      String to = opxright.getLine() + ":" + opxright.getColumn();
                                      String message = "This operator ("+op+") only works with integer subtypes" + " from "+from+" to "+to;
                                      throw new CompilerError(message, CompilerError.ErrorType.SEMANTIC, filenamePath);
                                    }
                                    op_code = Code.GE;
                                    break;
                                }

                                boolean allConst = val1.getIsConst() && val2.getIsConst();

                                Boolean val = null;
                                if(allConst){
                                  switch(op){
                                    case "==":
                                      if(subType1 == TipoSubyacente.TS_NUMBER){
                                        val = (Integer) val1.getValue() == (Integer) val2.getValue();
                                      }
                                      if(subType1 == TipoSubyacente.TS_STRING){
                                        val = ((String)val1.getValue()).equals((String) val2.getValue());
                                      }
                                      if(subType1 == TipoSubyacente.TS_BOOLEAN){
                                        val = (Boolean) val1.getValue() == (Boolean) val2.getValue();
                                      }
                                      break;
                                    case "!=":
                                      if(subType1 == TipoSubyacente.TS_NUMBER){
                                        val = (Integer) val1.getValue() != (Integer) val2.getValue();
                                      }
                                      if(subType1 == TipoSubyacente.TS_STRING){
                                        val = !((String)val1.getValue()).equals((String) val2.getValue());
                                      }
                                      if(subType1 == TipoSubyacente.TS_BOOLEAN){
                                        val = (Boolean) val1.getValue() != (Boolean) val2.getValue();
                                      }
                                      break;

                                    // only integer operators
                                    case "<":
                                      val = (Integer) val1.getValue() < (Integer) val2.getValue();
                                      break;
                                    case "<=":
                                      val = (Integer) val1.getValue() <= (Integer) val2.getValue();
                                      break;
                                    case ">":
                                      val = (Integer) val1.getValue() > (Integer) val2.getValue();
                                      break;
                                    case ">=":
                                      val = (Integer) val1.getValue() >= (Integer) val2.getValue();
                                      break;
                                  }
                                }

                                Type type_bool = symbolsTable.get("boolean");
                                String var_id = backTables.addTempVar(type_bool.getSize(), type_bool.getTipoSubyacente());

                                if(allConst){
                                  sym = new ValuesComparison(var_id, val);
                                }else{
                                  sym = new ValuesComparison(var_id);
                                }

                                c3a_g.generarC3A(op_code,val1.getVarId(),val2.getVarId(),var_id);

                                RESULT = sym;
                              }catch(SymTabError e){
                                throw new CompilerError(e.getMessage(), CompilerError.ErrorType.SEMANTIC, filenamePath);
                              }
                            :}
                            | LPAREN value_comparison:cmp_val RPAREN
                            {:
                              RESULT = cmp_val;
                            :};

cond                        ::= boolean_op_value:bool_val
                              {:
                                String var_id = bool_val.getVarId();

                                String true_label = backTables.addEtiq();
                                String false_label = backTables.addEtiq();

                                labelTrueStack.push(true_label);
                                labelFalseStack.push(false_label);


                                c3a_g.generarC3A(Code.jump_cond,var_id,"true",true_label);
                                c3a_g.generarC3A(Code.go_to,null,null,false_label);

                                RESULT = new Condition();
                              :};

cond_t                      ::=
                              {:
                                String label = labelTrueStack.pop();

                                c3a_g.generarC3A(Code.skip,null,null,label);

                                RESULT = new CondTrue();
                              :};

cond_f                      ::=
                              {:
                                String label = labelFalseStack.pop();

                                c3a_g.generarC3A(Code.skip,null,null,label);

                                RESULT = new CondFalse();
                              :};

instruction_if              ::= IF cond LBRACE block_in cond_t instructions block_out RBRACE instruction_elif instruction_else
                            {:
                              RESULT = new InstructionIf();
                            :};

m_end                       ::= EMPTY
                            {:
                              String label = backTables.addEtiq();

                              c3a_g.generarC3A(Code.go_to,null,null,label);

                              RESULT = new End(label);
                            :};

instruction_elif                ::= m_end:mend ELIF cond_f cond LBRACE block_in cond_t instructions block_out RBRACE instruction_elif
                            {:
                              String label = mend.getLabel();

                              c3a_g.generarC3A(Code.skip,null,null,label);

                              RESULT = new ElseIf();
                            :}
                            |
                            {:
                              RESULT = new ElseIf();
                            :};

instruction_else                        ::= m_end:mend ELSE LBRACE block_in cond_f instructions block_out RBRACE
                            {:
                              String label = mend.getLabel();

                              c3a_g.generarC3A(Code.skip,null,null,label);

                              RESULT = new Else();
                            :}
                            | cond_f
                            {:
                              RESULT = new Else();
                            :};

m_while                     ::= EMPTY
                            {:
                              String label = backTables.addEtiq();

                              c3a_g.generarC3A(Code.skip,null,null,label);

                              RESULT = new WhileLabel(label);
                            :};

instruction_while           ::= WHILE m_while:mwhile cond LBRACE block_in cond_t instructions block_out RBRACE
                            {:
                              String loop_label = mwhile.getLabel();
                              c3a_g.generarC3A(Code.go_to,null,null,loop_label);

                              String end_label = labelFalseStack.pop();
                              c3a_g.generarC3A(Code.skip,null,null,end_label);

                              RESULT = new InstructionWhile();
                            :};

instruction_for             ::= FOR LPAREN for_body:forBody RPAREN LBRACE block_in cond_t instructions block_out RBRACE
                            {:
                              String postInstrLabel = forBody.getLabel();
                              c3a_g.generarC3A(Code.go_to,null,null,postInstrLabel);
                              String end_label = labelFalseStack.pop();
                              c3a_g.generarC3A(Code.skip,null,null,end_label);
                              RESULT = new InstructionFor();
                            :};

label_for                   ::= EMPTY
                            {:
                              String label = backTables.addEtiq();

                              c3a_g.generarC3A(Code.skip,null,null,label);

                              RESULT = new ForLabel(label);
                            :};

for_body                    ::= declaration SEMICOLON label_for:labelFor cond SEMICOLON label_post_for:labelPostFor operator_assig
                                {:
                                    String iniForLabel = labelFor.getLabel();
                                    c3a_g.generarC3A(Code.go_to,null,null,iniForLabel);

                                    RESULT = new ForBody(labelPostFor.getLabel());
                                :};

label_post_for              ::= EMPTY
                            {:
                              String label = backTables.addEtiq();

                              c3a_g.generarC3A(Code.skip,null,null,label);
                              RESULT = new LabelPostFor(label);
                            :};

call_function               ::= ID:fun_id LPAREN:lp RPAREN:rp
                            {:
                              try{
                                // Check if function and has params
                                int num_params = symbolsTable.getNumParams(fun_id);
                                if(num_params > 0 ){
                                  String from = lpxleft.getLine() + ":" + lpxleft.getColumn();
                                  String to = rpxright.getLine() + ":" + rpxright.getColumn();
                                  String message = "Missing parameters. Function " + fun_id + " has " + num_params + " parameters" + " from "+from+" to "+to;
                                  throw new CompilerError(message, CompilerError.ErrorType.SEMANTIC, filenamePath);
                                }

                                Type fun_type = symbolsTable.get(fun_id);
                                String backId = fun_type.getBackendId();
                                c3a_g.generarC3A( Code.call, 0 + "", null, backId );

                                RESULT = new CallFunction(fun_id, backId);
                              }catch(SymTabError e){
                                String from = fun_idxleft.getLine() + ":" + fun_idxleft.getColumn();
                                String to = fun_idxright.getLine() + ":" + fun_idxright.getColumn();
                                String message = e.getMessage() + " from "+from+" to "+to;
                                throw new CompilerError(message, CompilerError.ErrorType.SEMANTIC, filenamePath);
                              }
                            :}
                            | call_body:body RPAREN:rp
                            {:
                              try{
                                int body_params = body.getNumParams();
                                String fun_id = body.getFunId();
                                int fun_params = symbolsTable.getNumParams(fun_id);

                                if(fun_params != body_params){
                                  String from = bodyxleft.getLine() + ":" + bodyxleft.getColumn();
                                  String to = rpxright.getLine() + ":" + rpxright.getColumn();
                                  String message = "Missing parameters. Function " + fun_id + " has " + fun_params + " parameters" + " from "+from+" to "+to;
                                  throw new CompilerError(message, CompilerError.ErrorType.SEMANTIC, filenamePath);
                                }

                                String fun_backId = body.getFunBackId();
                                c3a_g.generarC3A( Code.call, fun_params + "", null, fun_backId );

                                RESULT = new CallFunction(fun_id, fun_backId);
                              }catch(SymTabError e){
                                throw new CompilerError(e.getMessage(), CompilerError.ErrorType.SEMANTIC, filenamePath);
                              }
                            :};

call_body                   ::= call_body:body COMMA value:value
                            {:
                              try{
                                String fun_id = body.getFunId();
                                int num_params = body.getNumParams();
                                TipoSubyacente subType;

                                if(value.getTipo() == Tipo.dnull){
                                  subType = value.getTipoSubyacente();
                                }
                                else{
                                  if(value.getTipo() != Tipo.dvar && value.getTipo() != Tipo.dconst && value.getTipo() != Tipo.darg){
                                    String from = valuexleft.getLine() + ":" + valuexleft.getColumn();
                                    String to = valuexright.getLine() + ":" + valuexright.getColumn();
                                    String message = fun_id + ". Incorrect type for param "+ num_params + " from "+from+" to "+to;
                                    throw new CompilerError(message, CompilerError.ErrorType.SEMANTIC, filenamePath);
                                  }
                                  Type value_type = symbolsTable.get(value.getTypeName());
                                  subType = value_type.getTipoSubyacente();
                                }

                                //Check param type with function param type
                                Type paramType = symbolsTable.getParam(fun_id, num_params);
                                TipoSubyacente paramSubType = symbolsTable.get(paramType.getTypeName()).getTipoSubyacente();

                                if (paramSubType != subType){
                                  String from = valuexleft.getLine() + ":" + valuexleft.getColumn();
                                  String to = valuexright.getLine() + ":" + valuexright.getColumn();
                                  String message = fun_id + ". Incorrect type for param " + num_params + ". Expected " + subType + " but got " + paramSubType + " from "+from+" to "+to;
                                  throw new CompilerError(message, CompilerError.ErrorType.SEMANTIC, filenamePath);
                                }

                                String fun_back_id = body.getFunBackId();
                                c3a_g.generarC3A(Code.param,value.getVarId(),null,fun_back_id);

                                RESULT = new CallBody(fun_id, fun_back_id, num_params+1);
                              }catch(SymTabError e){
                                throw new CompilerError(e.getMessage(), CompilerError.ErrorType.SEMANTIC, filenamePath);
                              }
                            :}
                            | ID:fun_id LPAREN value:value
                            {:
                              try{
                                TipoSubyacente subType;

                                if(value.getTipo() == Tipo.dnull){
                                  subType = value.getTipoSubyacente();
                                }
                                else{
                                  if(value.getTipo() != Tipo.dvar && value.getTipo() != Tipo.dconst && value.getTipo() != Tipo.darg){
                                    String from = valuexleft.getLine() + ":" + valuexleft.getColumn();
                                    String to = valuexright.getLine() + ":" + valuexright.getColumn();
                                    String message = fun_id + ". Incorrect type for param 0" + " from "+from+" to "+to;
                                    throw new CompilerError(message, CompilerError.ErrorType.SEMANTIC, filenamePath);
                                  }
                                  Type value_type = symbolsTable.get(value.getTypeName());
                                  subType = value_type.getTipoSubyacente();
                                }

                                //Check param type with function param type
                                Type paramType = symbolsTable.getParam(fun_id, 0);
                                TipoSubyacente paramSubType = symbolsTable.get(paramType.getTypeName()).getTipoSubyacente();

                                if(paramSubType != subType){
                                  String from = valuexleft.getLine() + ":" + valuexleft.getColumn();
                                  String to = valuexright.getLine() + ":" + valuexright.getColumn();
                                  String message = fun_id + ". Incorrect type for param 0. Expected " + subType + " but got " + paramSubType + " from "+from+" to "+to;
                                  throw new CompilerError(message, CompilerError.ErrorType.SEMANTIC, filenamePath);
                                }

                                Type fun_type = symbolsTable.get(fun_id);
                                String fun_back_id = fun_type.getBackendId();

                                c3a_g.generarC3A(Code.param,value.getVarId(),null,fun_back_id);
                                RESULT = new CallBody(fun_id, fun_back_id, 0 + 1);
                              }catch(SymTabError e){
                                String from = fun_idxleft.getLine() + ":" + fun_idxleft.getColumn();
                                String to = fun_idxright.getLine() + ":" + fun_idxright.getColumn();
                                String message = e.getMessage() + " from "+from+" to "+to;
                                throw new CompilerError(message, CompilerError.ErrorType.SEMANTIC, filenamePath);
                              }
                            :};

instruction_out             ::= PRINT LPAREN value:value RPAREN
                            {:
                              try{
                                String var_id = value.getVarId();
                                TipoSubyacente subtype;

                                if(value.getTipo() == Tipo.dnull){
                                  subtype = value.getTipoSubyacente();
                                }else{
                                  Type var_type = symbolsTable.get(value.getTypeName());
                                  subtype = var_type.getTipoSubyacente();
                                }

                                if (value.getTipo() == Tipo.dnull && value.getTipoSubyacente() == TipoSubyacente.TS_STRING) {
                                  var_id = backTables.addTemporalStringVariable(value.getStringSize(), (String) value.getValue());
                                  c3a_g.generarC3A( Code.print, subtype.toString(), (String) value.getValue(), var_id );
                                } else {
                                  c3a_g.generarC3A(Code.print,subtype.toString(),null,var_id);
                                }
                                RESULT = new InstructionOut();
                              }catch(SymTabError e){
                                throw new CompilerError(e.getMessage(), CompilerError.ErrorType.SEMANTIC, filenamePath);
                              }
                            :};


EMPTY                       ::= {: RESULT = new Empty(); :};